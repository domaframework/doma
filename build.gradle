plugins {
    id 'com.diffplug.gradle.spotless' version '3.16.0'
    id 'java'
    id 'signing'
    id 'maven'
}

group = 'org.seasar.doma'
version = '2.24.0-SNAPSHOT'
sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
    encoding = 'UTF-8'
}

spotless {
    java {
        googleJavaFormat('1.6')
    }
}

compileJava {
    doFirst {
        ant.replaceregexp(
            match: /(private static final String VERSION = ")[^"]*(")/,
            replace: "\\1${version}\\2",
            encoding: encoding, 
            flags: 'g') {
                fileset(dir: '.') {
                    include(name: '**/Artifact.java')
                }
            }
        ant.replaceregexp(
            match: /("org.seasar.doma:doma:)[^"]*(")/,
            replace: "\\1${version}\\2",
            encoding: encoding, 
            flags: 'g') {
                fileset(dir: 'docs') {
                    include(name: '**/*.rst')
                }
            }
    }
    options.encoding = encoding
}

compileTestJava {
    options.encoding = encoding
    options.compilerArgs = ['-proc:none']
}

test {
    maxHeapSize = '1g'
    useJUnitPlatform()
}

javadoc {
    options.encoding = encoding
    options.charSet = encoding
    options.docEncoding = encoding
    options.links 'http://docs.oracle.com/javase/jp/8/docs/api/'
    options.use = true
    exclude '**/internal/**'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Doma', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
    maven {url 'http://maven.seasar.org/maven2'}
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
}

task sourcesJar (type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar (type : Jar, dependsOn : javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

build.dependsOn install

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment {MavenDeployment deployment ->
            signing.signPom(deployment)
        }
        repository (url : sonatypeUrl) {
            authentication (
                    userName : sonatypeUsername,
                    password : sonatypePassword)
        }
        snapshotRepository (url : sonatypeSnapshotUrl) {
            authentication (
                    userName : sonatypeUsername,
                    password : sonatypePassword)
        }
        pom.project {
            name project.name
            packaging 'jar'
            description 'DAO Oriented Database Mapping Framework for Java 8+'
            url projectUrl
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url githubUrl
                connection "scm:git:${githubUrl}"
                developerConnection "scm:git:${githubUrl}"
            }
            developers {
                developer {
                    id 'nakamura-to'
                    name 'Toshihiro Nakamura'
                    email 'toshihiro.nakamura@gmail.com'
                }
            }
        }
    }
}

apply from:'eclipse.gradle'